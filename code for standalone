_code = {
_mine = param[0];
_radius = param[1, 50];
_nRounds = param[2, 3];


_pillar = createSimpleObject ["A3\Structures_F_Exp\Signs\Traffic\Bollard_01_F.p3d", position _mine];
_pillar attachTo [_mine, [0,0,0]];
_pillar setObjectScale 0.66;

_propAlt = 0.3;
_propRadius = 0.05;
_angle = 360/_nRounds;

_propArray = [];
_mineModel =  "A3\Weapons_F\explosives\mine_AP_bouncing";
for "_i" from 0 to (_nRounds-1) do {
	_prop = createSimpleObject [_mineModel, position _mine];
	_prop attachTo [_mine, [_propRadius * cos(_angle * _i), _propRadius * sin(_angle * _i), _propAlt]];
	_propArray pushBack _prop;
};

_i = 0;
while {(alive _mine) and (_i < _nRounds)} do {
	private _nearEntities = _mine nearEntities _radius;

	private _nearvehicles = _nearEntities select {!(_x isKindOf "Man") and !(_x isKindOf "Air")};

	_nearvehicles = _nearvehicles select {speed _x > 10};

	_nearvehicles = _nearvehicles select {alive _x};

	if(count _nearvehicles > 0) then {
		_target = selectRandom _nearvehicles;
		_nearTanks = _nearvehicles select {(_x isKindOf "Tank")};
		if(count(_nearTanks) > 0) then {
			_target = selectRandom _nearTanks;
		};

		sleep random 2;
		
		_chosenProp = (_propArray select 0);
		detach _chosenProp;
		deleteVehicle _chosenProp;
		_propArray deleteAt (_propArray find _chosenProp);
		_i = (_i + 1);

		_pos = (position _mine);
		_pos set [0, (_pos select 0) + _propRadius * cos(_angle * _i)];
		_pos set [1, (_pos select 1) + _propRadius * sin(_angle * _i)];
		_pos set [2, (_pos select 2) + _propAlt];

		_grenade = "G_40mm_HE" createVehicle _pos;
		[_grenade, 90, 0] call BIS_fnc_setPitchBank;

		_v0 = 30;
		_dir = _grenade getDir _target;
		_tgtSpeed = velocity _target;

		_grenade setVelocityModelSpace [_v0 * sin(_dir) + (_tgtSpeed select 0), 
										_v0 * sin(60), 
										-_v0 * cos(_dir) - (_tgtSpeed select 1)];
		

		_pos = position _grenade;
		_time = time;
		while {(time - _time) < 3 and (_grenade distance2D _target > 5) and ((velocity _grenade) select 2 > 0)} do {
			_pos = getPosASL _grenade;
			sleep 0.05;
		};
		deleteVehicle _grenade;

		_exp = createMine ["APERSMine", _pos, [], 0];
		_exp setPosASL _pos;
		_exp setDamage 1;

		_type = "Sh_125mm_APFSDS";
		_shell = _type createVehicle _pos;
		_shell setPosASL _pos;
		_shell setDir (_shell getDir _target);

		_hTarget 	= (getPosASL _target) select 2;
		_hMissile 	= (getPosASL _shell) select 2;
		_deltaH 	= (_hMissile - _hTarget);
		_distance 	= _shell distance2d _target;
		_angle 		= _deltaH atan2 _distance;
		[_shell, -_angle, 0] call BIS_fnc_setPitchBank;

		_leadAngle = 2;
		_impSpeed = 350;
		_vertical = _impSpeed * sin(_leadAngle);
		_lateral = _impSpeed / 30;
		_shell setVelocityModelSpace [random[-_lateral,0,_lateral], _impSpeed, _vertical];

		if(alive _target) then {
			{
				group _x setbehaviour "COMBAT";
			}forEach crew _target;
		};

		sleep  3;
	};
	sleep 0.1;
};

detach _pillar;
deleteVehicle _pillar;
{
	detach _x;
	deleteVehicle _x;
}forEach _propArray;

deleteVehicle _mine;
};

this spawn _code;
